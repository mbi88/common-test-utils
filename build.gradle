plugins {
    id 'ru.vyarus.quality' version '3.3.0'
}

allprojects {
    apply plugin: 'ru.vyarus.quality'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        api group: 'org.testng', name: 'testng', version: '6.14.3'
        api group: 'org.json', name: 'json', version: '20180130'
        api group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'
    }
}

subprojects {
    test {
        useTestNG() {
            testLogging {
                events "passed", "skipped", "failed"
                exceptionFormat = 'full'
                showStandardStreams = false
            }

            options {
                setParallel('classes')
                setThreadCount(10)
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

quality {
    checkstyle = true
    pmd = true
    findbugs = true
    codenarc = true
    spotbugs = true
}

check.dependsOn jacocoTestReport

wrapper.gradleVersion = '5.2.1'