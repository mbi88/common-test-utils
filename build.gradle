import groovy.io.FileType

plugins {
    id 'ru.vyarus.quality' version '4.3.0'
}

apply plugin: 'java-library'
apply plugin: 'jacoco'

def suitesDir = 'src/test/resources/suites/'

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    api group: 'com.github.mbi88', name: 'json-assert', version: 'master-SNAPSHOT', changing: true
    api group: 'com.github.mbi88', name: 'json-validator', version: 'master-SNAPSHOT', changing: true
    api group: 'com.github.mbi88', name: 'http-request', version: 'master-SNAPSHOT', changing: true
    api group: 'com.github.mbi88', name: 'date-handler', version: 'master-SNAPSHOT', changing: true
    api group: 'com.github.mbi88', name: 'data-faker', version: 'master-SNAPSHOT', changing: true
    api group: 'org.testng', name: 'testng', version: '7.2.0'
    api group: 'org.json', name: 'json', version: '20200518'
    api group: 'io.rest-assured', name: 'rest-assured', version: '4.3.1'
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: '1.11.838'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'com.github.wnameless', name: 'json-flattener', version: '0.7.1'
}

test {
    useTestNG() {
        // Add test suites
        def suitesList = []
        def dir = new File(suitesDir)
        dir.eachFileRecurse(FileType.FILES) { file -> suitesList << file }
        suitesList.each { options.suites(it.path) }

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = false
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

quality {
    checkstyle = true
    pmd = true
    codenarc = true
    spotbugs = false
}

check.dependsOn jacocoTestReport