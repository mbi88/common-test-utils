import groovy.io.FileType

plugins {
    id 'ru.vyarus.quality' version '2.4.0'
}

group 'com.audienceproject'
version '1.0'

apply plugin: 'java'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

def suitesDir = 'src/test/resources/suites/'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile group: 'com.github.mbi88', name: 'json-assert', version: '0bbd4136a1'
    compile group: 'com.github.mbi88', name: 'json-validator', version: 'a77cfac'
    compile group: 'com.github.mbi88', name: 'http-request', version: '9074a02bec'
    compile group: 'com.github.mbi88', name: 'date-handler', version: 'de2416b'
    compile group: 'org.testng', name: 'testng', version: '6.14.2'
    compile group: 'org.json', name: 'json', version: '20180130'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: '1.11.312'
}

test {
    useTestNG() {
        def suitesList = []
        def dir = new File(suitesDir)
        dir.eachFileRecurse(FileType.FILES) { file -> suitesList << file }
        suitesList.each { options.suites(it.path) }
    }

    testLogging {
        showStandardStreams = false
        events "passed", "failed", "skipped"
        showExceptions true
        showCauses false
        showStackTraces false
        exceptionFormat "FULL"

        // Print summary
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, " +
                        "${result.successfulTestCount} " +
                        "successes, ${result.failedTestCount} " +
                        "failures, ${result.skippedTestCount} " +
                        "skipped)"
                def repeatLength = output.length()
                println('\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength))
            }
        }

        onOutput { descriptor, event ->
            print(event.message)
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

quality {
    checkstyle = true
    pmd = false
    findbugs = true
    codenarc = true
}